set dotenv-load := true
set export
set shell := ["bash", "-euo", "pipefail", "-c"]
set unstable := true

VIRTUAL_ENV := justfile_directory() + "/.venv"
UV_CACHE_DIR := justfile_directory() + "/.uv_cache"

# List all available commands
[private]
default:
    @just --list --list-submodules

sync:
    uv sync

format:
    uv run ruff format

lint:
    uv run ruff check

test:
    uv run pytest -q -n auto

typecheck:
    uv run ty check

corpus-sync:
    uv run python -m corpus.sync

corpus-test:
    uv run pytest -q -k corpus

# Validate a sample of corpus templates (runs as part of `just test` when corpus is present)
corpus-templates:
    uv run pytest -q -k corpus_template_validate_sample

# Validate the full corpus of templates (can be very slow)
corpus-templates-full:
    uv run pytest -q -n auto -k corpus_template_validate_full --corpus-templates-full --corpus-sample-per-entry=0

# Discovery helper (temporary clones in /tmp)
corpus-discover *ARGS:
    uv run python -m corpus.discover {{ARGS}}

# Optional: inspect a real Django project environment for installed `{% load %}`
# libraries. Requires a valid DJANGO_SETTINGS_MODULE / dependencies installed.
corpus-inspect-runtime *ARGS:
    uv run python -m corpus.inspect_runtime {{ARGS}}
